apply plugin: 'java'
apply plugin: 'spring-boot'
apply from: "./libraries.gradle"

project.ext {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.1.6.RELEASE"
    }
}



dependencies {
    // tag::jetty[]
    compile("org.springframework.boot:spring-boot-starter-web") {
        exclude module: "spring-boot-starter-tomcat"
    }
    compile("org.springframework.boot:spring-boot-starter-jetty")
    // end::jetty[]

    // tag::actuator[]
    compile("org.springframework.boot:spring-boot-starter-actuator")
    // end::actuator[]
}


allprojects {
    repositories {
        mavenCentral()
    }

    // YOU SHALL NOT PASS!
    configurations {
        all*.exclude module: 'commons-logging'
    }
}


subprojects { subProject ->
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    targetCompatibility = rootProject.sourceCompatibility
    sourceCompatibility = rootProject.targetCompatibility

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    // common dependencies for all projects
    dependencies {
        // logging
        compile libraries.logback

        // testing
        testCompile libraries.assertJ
        testCompile libraries.mockito
        testCompile libraries.junit
    }

    sourceSets {
        main {
            resources {
                srcDirs 'src/main/resources'
                srcDirs 'src/main/properties'
            }
        }

        test {
            java {
                srcDirs 'src/test/'
            }
        }
    }

    task createDirs {
        description =  "creates basic directory structure for given project"

        sourceSets*.java.srcDirs*.each {
            it.mkdirs()
        }

        sourceSets*.resources.srcDirs*.each {
            it.mkdirs()
        }
    }
}

// task for setting up gradle wrapper
task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}
